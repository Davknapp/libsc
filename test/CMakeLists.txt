set(tests allgather arrays builtin keyvalue notify reduce search sort sortb version)
if(NOT MSVC AND NOT (WIN32 AND CMAKE_C_COMPILER_ID STREQUAL Clang))
  list(APPEND tests io_sink)
endif()
if(SC_HAVE_STDLIB_H)
  check_symbol_exists(random stdlib.h SC_HAVE_RANDOM)
  check_symbol_exists(srandom stdlib.h SC_HAVE_SRANDOM)
  if(SC_HAVE_RANDOM AND SC_HAVE_SRANDOM)
    list(APPEND tests node_comm)
  endif()
endif()
if(SC_SIZEOF_LONG GREATER_EQUAL 8)
  list(APPEND tests helpers)
endif()

foreach(t ${tests})
  add_executable(test_${t} test_${t}.c)
  target_link_libraries(test_${t} PRIVATE sc)
  if(ZLIB_FOUND)
    target_link_libraries(test_${t} PRIVATE ZLIB::ZLIB)
  endif()
  if(MPI_FOUND)
    target_link_libraries(test_${t} PRIVATE MPI::MPI_C)
  endif()
  if(NOT WIN32)
    target_link_libraries(test_${t} PRIVATE m)
  endif()

  add_test(NAME sc:test:${t} COMMAND ${MPIEXEC_EXECUTABLE} ${MPIEXEC_NUMPROC_FLAG} ${MPIEXEC_MAX_NUMPROCS} $<TARGET_FILE:test_${t}>)
endforeach()
